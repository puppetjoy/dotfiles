#!/usr/bin/env zsh
#
# Simple privilege escalation
#
# Provide a consistent sudo-like way to escalate
# privileges across a variety of platforms.
#

# quote each command argument
quoted_command=(${(q)argv})

if [[ $OS == 'Windows_NT' ]]; then
    shell_path="${SHELL:-/bin/zsh}"
    if [[ $shell_path != *.exe && -x "${shell_path}.exe" ]]; then
        shell_path="${shell_path}.exe"
    fi

    shell_exe="$(cygpath -am "$shell_path")"
    wt_profile_shell='{6c6d2be2-a7a8-4a38-bf3b-d0dd57f53af2}'
    wt_profile_command='{d5dbf8f2-3cea-4f62-8d88-4ba4f43cf6ca}'

    if [[ $# -eq 0 ]]; then
        exec cygstart --action=runas wt.exe --profile "$wt_profile_shell"
    else
        command_string="${(qq)quoted_command}"
        exec cygstart --action=runas wt.exe --profile "$wt_profile_command" "$shell_exe" -ilc "$command_string"
    fi
elif [[ $OSTYPE == darwin* ]]; then
    exec sudo -E "${@:--s}"
else
    exec sudo "${@:--s}"
fi
